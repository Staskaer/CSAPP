```
# Execution begins at address 0 
	.pos 0
	irmovq stack, %rsp  	# Set up stack pointer
	call main		# Execute main program
	halt			# Terminate program 

# Array of 8 elements
	.align 8
vals:	
	.quad 0x000000000000
	.quad 0x000000000000
	.quad 0x000000000000
	.quad 0x000000000000
	.quad 0x000000000000
	.quad 0x000000000000
	.quad 0x000000000000
	.quad 0x000000000000

jump_table:
	.quad L1
	.quad L4
	.quad L2
	.quad L3
	.quad L4
	.quad L2

main:	
	irmovq	vals, %r12

	irmovq	$-1,%rdi
	call switchv		# switchv(-1)
	rmmovq	%rax, (%r12)

	irmovq	$0,%rdi
	call switchv		# switchv(0)
	rmmovq	%rax, 0x8(%r12)

	irmovq	$1,%rdi
	call switchv		# switchv(1)
	rmmovq	%rax, 0x10(%r12)

	irmovq	$2,%rdi
	call switchv		# switchv(2)
	rmmovq	%rax, 0x18(%r12)

	irmovq	$3,%rdi
	call switchv		# switchv(3)
	rmmovq	%rax, 0x20(%r12)

	irmovq	$4,%rdi
	call switchv		# switchv(4)
	rmmovq	%rax, 0x28(%r12)

	irmovq	$5,%rdi
	call switchv		# switchv(5)
	rmmovq	%rax, 0x30(%r12)

	irmovq	$6,%rdi
	call switchv		# switchv(6)
	rmmovq	%rax, 0x38(%r12)

	ret

# long switchv(long idx)
# idx in %rdi
switchv:
	rrmovq %rdi, %r8
	irmovq $5, %r9
	subq %r9, %r8
	jg L4
	andq %rdi, %rdi
	jl L4
	irmovq jump_table, %r8
	irmovq $8, %r9
	irmovq $1, %r10
loop:
	subq %r10, %rdi
	jl endloop
	addq %r9, %r8
	jmp loop
endloop:
	mrmovq (%r8), %r8
	pushq %r8
	ret 

L1:				# case 0
	irmovq 0xaaa, %rax
	ret
L2:				#case 2 or case 5
	irmovq 0xbbb, %rax
	ret
L3:
	irmovq 0xccc, %rax	#case 3
	ret
L4:
	irmovq 0xddd, %rax	#default
	ret

# Stack starts here and grows to lower addresses
	.pos 0x400
stack:


```